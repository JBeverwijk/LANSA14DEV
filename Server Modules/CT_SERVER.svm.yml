ServerModule:
  Name:               CT_Server
  Target Platform:    Windows/IBM i/Linux

  Multilingual Details:
  - ENG:
      Description:        "Server Module"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_SRVM)
    
    Def_List Name(#ContactList) Fields(#xContactIdentification #xContactLastName #xContactFirstName #xContactEmailAddress #xContactMobilePhone #xContactPhoto #xContactCity #xContactAddress) Type(*WORKING) Entrys(*MAX)
    
    Group_By Name(#ContactFields) Fields(#xContactIdentification #xContactTitle #xContactLastName #xContactFirstName #xContactGender #xContactEmailAddress #xContactMobilePhone #xContactHomePhone #xContactFaxNumber #xContactBusinessPhone #xContactNotes #xContactPhoto #xContactInterests #xContactBirthdate #xContactHometown #xContactCity #xContactJobTitle #xContactCompany #xContactAddress #xContactCompanySite)
    
    Define Field(#CT_UserName) Type(*STRING)
    Define Field(#CT_Password) Type(*STRING)
    Define Field(#CT_ReturnCode) Type(*CHAR) Length(2)
    
    Srvroutine Name(SignIn)
    Field_Map For(*INPUT) Field(#CT_UserName) Parameter_Name(UserName)
    Field_Map For(*INPUT) Field(#CT_Password) Parameter_Name(Password)
    Field_Map For(*OUTPUT) Field(#CT_ReturnCode) Parameter_Name(ReturnCode)
    
    * TODO: Check username and password
    
    #COM_OWNER.StartSession Timeout(3600)
    
    #CT_ReturnCode := OK
    
    Endroutine
    
    Srvroutine Name(SignOut) Session(*REQUIRED)
    
    #COM_OWNER.EndSession
    
    Endroutine
    
    Srvroutine Name(Find) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#STD_STRNG) Parameter_Name(Filter)
    List_Map For(*OUTPUT) List(#ContactList)
    
    #STD_STRNG := #STD_STRNG.UpperCase.Trim
    
    Select Fields(#ContactList) From_File(xContacts) Io_Error(*NEXT) Val_Error(*NEXT)
    
    If ((#STD_STRNG = *BLANKS) *OrIf (#xContactTitle.UpperCase.Contains( #STD_STRNG )) *OrIf (#xContactLastName.UpperCase.Contains( #STD_STRNG )) *OrIf (#xContactFirstName.UpperCase.Contains( #STD_STRNG )))
    
    Add_Entry To_List(#ContactList)
    
    Endif
    
    Endselect
    
    Endroutine
    
    Srvroutine Name(Read) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#xContactIdentification)
    Group_Map For(*OUTPUT) Group(#ContactFields)
    
    Fetch Fields(#ContactFields) From_File(xContacts) With_Key(#xContactIdentification) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endroutine
    
    Srvroutine Name(Save) Session(*REQUIRED)
    Group_Map For(*INPUT) Group(#ContactFields)
    Field_Map For(*INPUT) Field(#STD_BLOB)
    Field_Map For(*OUTPUT) Field(#STD_CODE) Parameter_Name(ReturnCode)
    
    Define_Com Class(#PRIM_STR) Name(#FileName)
    
    #STD_CODE := "OK"
    
    Update Fields(#ContactFields) In_File(xContacts) With_Key(#xContactIdentification) Io_Error(*NEXT) Val_Error(*NEXT)
    
    If_Status Is(*NORECORD)
    
    Insert Fields(#ContactFields) To_File(xContacts) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endif
    
    If_Status Is(*VALERROR)
    
    #STD_CODE := "VE"
    
    Return
    
    Endif
    
    If (#STD_BLOB.IsNotSqlNull)
    
    #FileName := (*PART_DIR + "web\vl\images\contacts\" + #xContactPhoto)
    
    Use Builtin(OV_FILE_SERVICE) With_Args("COPY_FILE" #STD_BLOB.FileName #FileName.AsNativeString)
    
    Endif
    
    Endroutine
    
    Srvroutine Name(Delete) Session(*REQUIRED)
    Field_Map For(*INPUT) Field(#xContactIdentification)
    
    Delete From_File(xContacts) With_Key(#xContactIdentification) Io_Error(*NEXT) Val_Error(*NEXT)
    
    Endroutine
    
    End_Com
